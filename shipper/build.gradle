plugins {
    id 'java'
    id 'antlr'
    id "nebula.rpm" version "8.4.2"
}

repositories {
    jcenter()
}

// I prefer unit tests with files on side
sourceSets.test.resources.srcDir (['src/test/java','src/test/resources'])

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
    outputDirectory = new File("${project.buildDir}/generated-src/antlr/org/logstash".toString())
}

task fatJar(type: Jar, dependsOn: 'jar') {
    manifest {
        attributes 'Main-Class': 'shipper.Shipper'
    }
    baseName = 'shipper-fat'
    from { 
      configurations.runtimeClasspath.collect { 
         it.isDirectory() ? it : zipTree(it) 
      }
    }
    from {
      	"build/classes/java/main" 
    }
    from {
        "build/resources/main/"
    }
}

task rpm(type: Rpm, dependsOn: 'fatJar') {
    release '1'
    os = LINUX
    type = BINARY
    postInstall file('rpm/postInstall.sh')
    user 'root'
    fileMode 0644
    license 'Apache License'
    packageDescription 'logstash (wannabe) compatible log shipper'
    requires('adoptopenjdk-15-hotspot-jre')
    vendor 'Foosoft Srl'
    from(fatJar.outputs.files) {
        into '/usr/share/shipper'
    }
    from('rpm/etc') {
        addParentDirs false
        fileType CONFIG | NOREPLACE
        into '/etc'
    }
    from('rpm/usr') {
        addParentDirs false
        into '/usr'
    }
    from('rpm/usr_bin') {
        addParentDirs false
        into '/usr/bin'
        fileMode 0755
    }
    from('rpm/lib') {
        addParentDirs false
        into '/usr/share/shipper'
    }
}


dependencies {
    antlr "org.antlr:antlr4:4.9.1" // use ANTLR version 4
    implementation project(path: ":shipper-api")
    implementation project(path: ":shipper-plugins")
    implementation project(path: ":shipper-core")
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.1'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.12.1'
    implementation group: 'info.picocli', name: 'picocli', version: '4.6.1'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
}
